plugins {
    /* You can check and update plugin versions at:
    https://fabricmc.net/develop/
    https://github.com/Fallen-Breath/preprocessor
    https://github.com/Fallen-Breath/yamlang */

    id 'fabric-loom' version '1.4-SNAPSHOT' apply false
    id 'com.replaymod.preprocess' version '20c7ec554a'
    id 'me.fallenbreath.yamlang' version '1.3.0' apply false
    id 'maven-publish'
}

preprocess {
    def mc194 = createNode('1.19.4', 1_19_04, 'quilt')
    def mc201 = createNode('1.20.1', 1_20_01, 'quilt')
    def mc202 = createNode('1.20.2', 1_20_02, 'quilt')

    mc202.link(mc201, null)
    mc201.link(mc194, null)
}

tasks.register('buildAndGather') {
    subprojects {
        dependsOn project.tasks.named('build').get()
    }
    doFirst {
        println 'Gathering builds'
        def buildLibs = {
            p -> p.buildDir.toPath().resolve('libs')
        }
        delete fileTree(buildLibs(rootProject)) {
            include '*'
        }
        subprojects {
            copy {
                from(buildLibs(project)) {
                    include '*.jar'
                    exclude '*-dev.jar', '*-sources.jar'
                }
                into buildLibs(rootProject)
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
}